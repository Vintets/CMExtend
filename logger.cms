//#name "logger"
// Author: Vint
// Version: 1.2 (30.09.2021)
// Скрипт для Clickermann v4.13.014
// подключение logger:
// #include "..\Libs\logger.cms"

#define @CR:STRCONCAT(char(13),char(10))

//==============================================================================
//===  logXY вывод координат в лог  ============================================
SUB(logXY, $coordX,$coordY)
    // Пример вызова:
    // logXY($_return1,$_return2)
    LOGWRITE("X = ", $coordX, ", Y = ", $coordY)
END_SUB

//==============================================================================
//===  Вывод времени выполнения ms в формате час:мин:сек,мс ====================
SUB(timeprint, $tt_print)
    // Пример вызова:
    // timeprint(12345)
    IF($tt_print > 999)
        $timesec = $tt_print/1000
        $hours = int($timesec/3600)
        $minuts = int(($timesec - ($hours*3600))/60)
        $sec = ($tt_print - ($hours*3600*1000) - ($minuts*60*1000)) / 1000
        LOGWRITE($hours, ":" , $minuts , ":" , $sec)
    END_IF
END_SUB

//==============================================================================
//===  Вывод времени выполнения  ===============================================
SUB(print_timeaction, $actionnamet, $tt_search)
    // Пример вызова:
    // $timer1 = $_ms
    // ...
    // print_timeaction("тест 1", $_ms - $timer1)
    LOGWRITE("===============  время выполнения  ===============")
    LOGWRITE("время выполнения ", $actionnamet, " : ", $tt_search, " мс")
    timeprint($tt_search)
    LOGWRITE(" ")
END_SUB

//==============================================================================
//===  LOG_ARR вывод массива в лог  ============================================
SUB(LOG_ARR, $ARR_LOG)
    // Пример вызова:
    // LOG_ARR($ARR)
    LOGWRITE("Размер массива: ", ARRSIZE($ARR_LOG))
    FOR($INARR,$INARR < ARRSIZE($ARR_LOG))
        LOGWRITE("[",$INARR,"] = ", $ARR_LOG[$INARR])
    END_CYC
    LOGWRITE(" ")
END_SUB

//==============================================================================
//===  LOG_ARR2 вывод парных координат из массива в лог  =======================
SUB(LOG_ARR2, $ARR_LOG)
    // Пример вызова:
    // LOG_ARR2($ARR)
    LOGWRITE("Наборов координат: ", ARRSIZE($ARR_LOG)/2)
    FOR($INARR,$INARR < ARRSIZE($ARR_LOG), 2)
        LOGWRITE($INARR," Координаты X/Y:  ", $ARR_LOG[$INARR], " / ", $ARR_LOG[$INARR+1])
    END_CYC
    LOGWRITE(" ")
END_SUB

//==============================================================================
//===  LOG_DATE вывод лог  с датой и временем ==================================
SUB(LOG_DATE, $note)
    // Пример вызова:
    // LOG_DATE("Примечание")
    $filelog = "LOG.txt"
    UNDEFINE($timedata)
    ARRPUSH($timedata, $_time_h)
    ARRPUSH($timedata, $_time_m)
    ARRPUSH($timedata, $_time_s)
    ARRPUSH($timedata, $_date_d)
    ARRPUSH($timedata, $_date_m)
    ARRPUSH($timedata, $_date_y)
    
    FOR($i_t=0, $i_t < 6)
        IF($timedata[$i_t] < 10)
            $timedata[$i_t] = STRCONCAT ("0", $timedata[$i_t])
        END_IF
    END_CYC
    
    //LOGWRITE($note)
    LOGWRITE($timedata[5],".",$timedata[4],".",$timedata[3]," ",$timedata[0],":",$timedata[1],":",$timedata[2], " ", $note)
    TFWRITE($filelog, STRCONCAT($dd,".",$dm,".",$dy," ",$th,":",$tm,":",$ts, " ", $note))
END_SUB

//==============================================================================

